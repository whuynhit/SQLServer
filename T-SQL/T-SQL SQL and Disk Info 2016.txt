-- Queries SQL Server and Disk Information; Works for SQL Server 2016, 2017, 2019, 2022
DECLARE @ServerName NVARCHAR(128) = UPPER(CAST(SERVERPROPERTY('MachineName') AS nvarchar(128)));
DECLARE @InstanceName NVARCHAR(128) = UPPER(CAST(SERVERPROPERTY('ServerName') AS nvarchar(128)));
DECLARE @ProductVersion NVARCHAR(128) = CONVERT(NVARCHAR, SERVERPROPERTY('ProductVersion'));
DECLARE @DriveE AS VARCHAR(3) = 'E:\';
DECLARE @DriveF AS VARCHAR(3) = 'F:\';

With DiskInfo AS (
SELECT DISTINCT
	vs.volume_mount_point,
	vs.total_bytes,
	vs.available_bytes
FROM sys.master_files mf
CROSS APPLY sys.dm_os_volume_stats(mf.database_id, mf.file_id) vs
WHERE vs.volume_mount_point IN (@DriveE, @DriveF)
)

SELECT 
@ServerName AS [Server_Name],
@InstanceName AS [Instance_Name],
(SELECT dec.local_net_address 
FROM sys.dm_exec_connections AS dec 
WHERE dec.session_id = @@SPID) AS [IP_Address],

CASE
WHEN @ServerName LIKE '%P[0-9][0-9]%' THEN 'PRD'
WHEN @ServerName LIKE '%T[0-9][0-9]%' THEN 'UAT'
WHEN @ServerName LIKE '%D[0-9][0-9]%' THEN 'DEV'
WHEN @ServerName LIKE '%DEV[0-9]%' THEN 'DEV'
ELSE 'Unknown ENV'
END AS [ENV],

CASE
WHEN @ServerName LIKE '%AWS%' THEN 'AWS EC2'
WHEN @ServerName LIKE '%ABY%' THEN 'AWS EC2'
ELSE 'On-Prem'
END AS [Location],

CASE
WHEN @ProductVersion LIKE '16%' THEN 'SQL Server 2022' 
WHEN @ProductVersion LIKE '15%' THEN 'SQL Server 2019'
WHEN @ProductVersion LIKE '14%' THEN 'SQL Server 2017'
WHEN @ProductVersion LIKE '13%' THEN 'SQL Server 2016'
ELSE 'Unknown SQL Server Version'
END AS [SQL_Version],

SERVERPROPERTY('Edition') AS [Edition],
@ProductVersion AS [Product_Version],
(SELECT cpu_count FROM sys.dm_os_sys_info) AS [CPU_Count],

-- Drive E, Data Storage
CAST(SUM(CASE WHEN volume_mount_point = @DriveE
THEN (total_bytes - available_bytes) END) 
/ 1024.0 / 1024.0/ 1024.0 AS DECIMAL(10,2)) AS [DriveE_UsedSpaceGB],
CAST(SUM(CASE WHEN volume_mount_point = @DriveE
THEN (available_bytes) END) 
/ 1024.0 / 1024.0/ 1024.0 AS DECIMAL(10,2)) AS [DriveE_FreeSpaceGB],
CAST(SUM(CASE WHEN volume_mount_point = @DriveE
THEN (total_bytes) END) 
/ 1024.0 / 1024.0/ 1024.0 AS DECIMAL(10,2)) AS [DriveE_TotalSpaceGB],

-- Drive F, Data Storage
CAST(SUM(CASE WHEN volume_mount_point = @DriveF
THEN (total_bytes - available_bytes) END) 
/ 1024.0 / 1024.0/ 1024.0 AS DECIMAL(10,2)) AS [DriveF_UsedSpaceGB],
CAST(SUM(CASE WHEN volume_mount_point = @DriveF
THEN (available_bytes) END) 
/ 1024.0 / 1024.0/ 1024.0 AS DECIMAL(10,2)) AS [DriveF_FreeSpaceGB],
CAST(SUM(CASE WHEN volume_mount_point = @DriveF
THEN (total_bytes) END) 
/ 1024.0 / 1024.0/ 1024.0 AS DECIMAL(10,2)) AS [DriveF_TotalSpaceGB]
FROM DiskInfo;